"""""""""""""""""""""""""""""""""""""""""""""""""""""""
" vimrc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Initializing {{{

" Skip initialization if '+eval' feature is disabled. {{{
" Note: (see: :help no-eval-feature)
"   In '-eval' environment, the argument of 'if' (including '| endif')
"   is ignored. So all lines after 'if' are ignored too. But
"   backslashes of line-continuation are still recognized and
"   cause errors when 'compatible' option is set. So
"   setting 'cpoptions' to enable line-continuation before
"   'if' statement makes sure all lines get ignored without error.
" }}}
set cpoptions-=C | if 0 | endif

" Default properties
let g:is_windows = has('win32') || has('win64')
let g:is_mac     = has('mac')   || has('macunix') || has('gui_macvim')
let g:is_gui     = has('gui_running')
let $MYVIMDIR    = expand( g:is_windows ? '~/vimfiles' : '~/.vim' )
let $VIMLOCAL    = expand($MYVIMDIR . '/local')

" Reset all autocmd defined in vimrc.
" TODO: create ex command to define vimrc autocmd.
augroup vimrc
  autocmd!
augroup END

if has('vim_starting')
  autocmd vimrc VimEnter * call <SID>display_startup_time()

  let s:startuptime = reltime()
  function! s:display_startup_time()
    echomsg 'Startup time:' reltimestr( reltime(s:startuptime) )
    unlet s:startuptime
  endfunction
endif

" }}}

" Colorscheme {{{

augroup vimrc
  autocmd ColorScheme hybrid highlight LineNr ctermfg=14 guifg=#676b41
augroup END

if has('vim_starting')
  set t_Co =256
  if &t_Co < 256
    colorscheme desert
  else
    colorscheme smyck256
  endif
endif

" }}}

" Commands and functions {{{

" SID {{{
function! s:SID()
  return matchstr(expand('<sfile>'), '<SNR>\zs\d\+\ze_SID$')
endfunction
" }}}

" Reload and Edit .vimrc {{{
command! Rv  source $MYVIMRC | if g:is_gui | source $MYGVIMRC | endif
command! Ev  edit   $MYVIMRC
command! Evg edit   $MYGVIMRC
" }}}

" Set indent easily {{{
command! -nargs=* IndentBy call <SID>set_indent(<f-args>)
command! ShortIndent  IndentBy 2 1
command! MediumIndent IndentBy 4 1

function! s:set_indent(n_space, expand_tab)
  let &l:shiftwidth  = a:n_space
  let &l:tabstop     = a:n_space
  let &l:softtabstop = a:n_space
  let &l:expandtab   = a:expand_tab
endfunction
" }}}

" }}}

" Basic options {{{

language C
set helplang =en,ja

set encoding      =utf-8
set fileencoding  =utf-8
set fileencodings =utf-8,cp932

if exists('&ambiwidth')
  set ambiwidth =double
endif

filetype plugin indent on
syntax on

" Default tab/space settings.
set tabstop     =4
set shiftwidth  =4
set softtabstop =4
set expandtab
set autoindent
set smartindent

" Search settings.
set hlsearch
set incsearch
set ignorecase
set smartcase

" Avoid highlighting the last search pattern at reloading vimrc.
nohlsearch

" Disable file backups.
set nobackup
set nowritebackup
set noswapfile

if has('persistent_undo')
  if ! isdirectory($VIMLOCAL . '/undo')
    call mkdir($VIMLOCAL . '/undo')
  endif
  set undofile
  set undodir =$VIMLOCAL/undo
endif

" Briefly jump to the matching bracket.
set showmatch
set matchtime =2
set matchpairs& matchpairs+=<:>

" Folds settings.
set foldmethod =marker
set foldcolumn =3

set cursorline
set number
set relativenumber
set hidden
set ruler
set showcmd
set showmode
set autoread
set nostartofline
set title
set wildmenu
set list
set listchars   =tab:>\ "
set backspace   =indent,eol,start
set linespace   =1
set mouse       =a
set clipboard   =unnamed
set keywordprg  =:help
set scrolloff   =3
set textwidth   =0
set history     =50
set laststatus  =2
set cmdheight   =2
set completeopt =longest,menuone
set whichwrap   =b,s,<,>,[,]
set statusline  =%f%m%r%h%w\ -\ [%{(&fenc!=''?&fenc:&enc)}\ %{&ff}\ %Y]\ [%Llines\]\ (%04l,%04v)
set formatoptions =croql

" Indent counts of leading backslash for line continuations in vim script.
let g:vim_indent_cont = 2

augroup vimrc
  autocmd WinEnter * checktime
augroup END

" }}}

" Filetype settings {{{

" Options for each file type {{{
augroup vimrc
  " Groovy local settings
  autocmd FileType groovy setlocal cindent cinoptions& cinoptions+=j1

  " *.gradle => grooby
  autocmd BufNewFile,BufRead *.gradle setlocal filetype=groovy

  " Show relative line numbers in help files.
  autocmd FileType help setlocal relativenumber
augroup END
" }}}

" Indent settings {{{
augroup vimrc
  autocmd FileType javascript ShortIndent
  autocmd FileType coffee     ShortIndent
  autocmd FileType css        ShortIndent
  autocmd FileType scss       ShortIndent
  autocmd FileType sass       ShortIndent
  autocmd FileType haml       ShortIndent
  autocmd FileType yaml       ShortIndent
  autocmd FileType ruby       ShortIndent
  autocmd FileType vim        ShortIndent
  autocmd FileType c          MediumIndent
  autocmd FileType cs         MediumIndent
  autocmd FileType vb         MediumIndent
  autocmd FileType java       MediumIndent
  autocmd FileType groovy     MediumIndent
  autocmd FileType xml        MediumIndent
  autocmd FileType html       MediumIndent
  autocmd FileType xhtml      MediumIndent
  autocmd FileType eruby      MediumIndent
  autocmd FileType sh         MediumIndent
  autocmd FileType markdown   MediumIndent
augroup END
" }}}

if ! has('vim_starting')
  " Apply file type settings to the current buffer when vimrc is reloaded.
  doautocmd vimrc FileType
endif
" }}}

" Key mappings {{{

" Use custom mapping commands.
call w#mapping#load(s:SID())

" Define 'mapleader' before all mappings usiing <Leader>.
let mapleader = "-"
Map nv - <Nop>

" Disable these keys to use as the main leader keys.
Map n m       <Nop>
Map n q       <Nop>
Map n <Space> <Nop>

" Motion {{{

" Numbers {{{

" Invert numbers by <Space> (to type 6 - 9 by left hand).
for s:n in range(1, 9)
  execute 'Map nvo <Space>' . s:n . ' ' . (10 - s:n)
endfor
unlet s:n

" }}}

" Insert mode {{{

Map i <C-j> <Down>
Map i <C-k> <Up>
Map i <C-h> <Left>
Map i <C-l> <Right>
Map i <C-a> <Home>
Map i <C-e> <End>
Remap i <S-CR> <End><CR>
Remap i <C-CR> <C-o>O

" Break undo sequence after these deletions in Insert mode.
Map i <C-w> <C-g>u<C-w>
Map i <C-u> <C-g>u<C-u>

" }}}

" Normal mode {{{

" Marks {{{
Map n mm m
Map n _  `
" }}}

" Fix the direction of the ', ',', 'n', 'N'. {{{

" Make the ';' key always move to the right.
" Make the ',' key always move to the left.
Map nvo (expr) f <SID>map_repeat_keys_and_move_to_occurrence(1, 'f')
Map nvo (expr) F <SID>map_repeat_keys_and_move_to_occurrence(0, 'F')
Map nvo (expr) t <SID>map_repeat_keys_and_move_to_occurrence(1, 't')
Map nvo (expr) T <SID>map_repeat_keys_and_move_to_occurrence(0, 'T')

" Make the 'n' key always move forward.
" Make the 'N' key always move backward.
Map nvo (expr) n <SID>search_pattern_to_fixed_direction('n', 'N')
Map nvo (expr) N <SID>search_pattern_to_fixed_direction('N', 'n')

function! s:map_repeat_keys_and_move_to_occurrence(direct_to_right, command)
  if a:direct_to_right
    Map n ; ;
    Map n , ,
  else
    Map n ; ,
    Map n , ;
  endif
  return a:command
endfunction

function! s:search_pattern_to_fixed_direction(normal_key, reverse_key)
  return v:searchforward ? a:normal_key : a:reverse_key
endfunction

" }}}

" }}}

" Visual mode {{{

" Reselect visual block after indent.
Map v < <gv
Map v > >gv

" }}}

" Command-line mode {{{

" Like emacs.
Map c <C-a> <Home>
Map c <C-b> <Left>
Map c <C-e> <End>
Map c <C-f> <Right>
Map c <C-n> <Down>
Map c <C-p> <Up>

" }}}

" }}}

" Action {{{

" TODO: Define own 'unimpaired' mappings.

" Map text objects. {{{

MapTextObj d "
MapTextObj s '
MapTextObj m )
MapTextObj n }
MapTextObj y >

" }}}

" Toggle options. {{{

MapNamedKey toggle qo
MapToggle r relativenumber
MapToggle n number
MapToggle w wrap
MapToggle t expandtab
MapToggle l list

" }}}

" Customize operations in normal mode. {{{
Map n <C-h>     :us:vert help
Map n <Leader>h :us:help
Map n <Space>w  ::update
Map n <Space>W  ::update!
Map n <Space>q  ::quit
Map n <Space>Q  ::quit!
Map n <C-j> <C-e>gj
Map n <C-k> <C-y>gk
Map n Y y$
Map n Q q
" }}}

" Disallow delete operations to change clipboard except 'd'. {{{
for s:lkey in ['c', 's', 'x']
  let s:ukey = toupper(s:lkey)
  execute 'Map nv' s:lkey '"_' . s:lkey
  execute 'Map nv' s:ukey '"_' . s:ukey
endfor

" Leave default operations as 'm[key]'
for s:lkey in ['c', 'x']
  let s:ukey = toupper(s:lkey)
  execute 'Map nv' 'm' . s:lkey s:lkey
  execute 'Map nv' 'm' . s:ukey s:ukey
endfor
unlet s:lkey s:ukey

" The 'md' command delete texts without copy. 
Map nv md "_d
Map nv md "_d
" }}}

" Break lines and Insert spaces in normal mode. {{{
Map n <S-CR>          o<Esc>
Map n <C-CR>          O<Esc>
Map n <S-Space>       i<Space><Esc>
Map n <Leader><Space> a<Space><Esc>
" }}}

" Paste texts smartly. {{{
Map i (silent) <C-v> <C-o>:set paste<CR><C-r>*<C-o>:set nopaste<CR>
Map c <C-v> <C-r>*
" }}}

" }}}

" Buffers {{{

MapNamedKey buffer <Space>b
Map n \[buffer]a ::buffer #
Map n \[buffer]d ::bdelete
Map n \[buffer]l ::ls
Map n \[buffer]s :us:ls<CR>:buffer
Map n \[buffer]j ::execute 'buffer' v:count1
" }}}

" Tabs {{{

" Use 't' as the prefix of tab motions.
MapNamedKey tab t
Map n \[tab]n ::tabnew
Map n \[tab]h gT
Map n \[tab]l gt
" TODO: mappings for tab motions

" }}}

" Others {{{

" In command mode, paste current path by '%%'.
Map c (expr) %% getcmdtype() == ':' ? expand('%:h') : '%%'

" Reset search highlights.
Map n (silent) <C-l> :<C-u>nohlsearch<CR><C-l>

" Omni completion settings
Map i <C-Space> <C-x><C-o>
Map i (expr) <TAB> pumvisible() ? '<C-n>' : '<TAB>'

" }}}

" }}}

" Plugins {{{

call w#neobundle#wrap()

" Declare bundles {{{
function! s:declare_bundles()

  " Extension {{{

  NeoBundlew 'Shougo/vimproc.vim', {
    \ 'disabled': g:is_windows,
    \ 'build': {
    \   'mac'   : 'make -f make_mac.mak',
    \   'linux' : 'make',
    \   'unix'  : 'gmake'
    \   }
    \ }

  NeoBundlew 'Shougo/unite.vim'

  NeoBundlew 'Shougo/neomru.vim', {
    \ 'depends': ['unite']
    \ }

  NeoBundlew 'Shougo/vimfiler.vim', {
    \ 'depends': ['unite']
    \ }

  NeoBundlewLazy 'Shougo/vimshell.vim', {
    \ 'depends': ['vimproc']
    \ }

  NeoBundlew 'Shougo/tabpagebuffer.vim'

  NeoBundlew 'kana/vim-tabpagecd'

  NeoBundlew 'kana/vim-submode'

  NeoBundlew 'tpope/vim-repeat'

  " }}}

  " Editing {{{

  " Basic {{{
  NeoBundlew 'junegunn/vim-easy-align'

  NeoBundlew 'tpope/vim-commentary'

  NeoBundlew 'tpope/vim-surround'

  NeoBundlew 'kana/vim-smartinput'

  NeoBundlew 'cohama/vim-smartinput-endwise'

  NeoBundlew 'LeafCage/yankround.vim'
  " }}}

  " Text object {{{
  NeoBundlew 'kana/vim-textobj-user'

  NeoBundlew 'kana/vim-textobj-entire'

  NeoBundlew 'kana/vim-textobj-line'

  NeoBundlew 'kana/vim-textobj-indent'
  " }}}

  " Operator {{{
  NeoBundlew 'kana/vim-operator-user'

  NeoBundlew 'kana/vim-operator-replace'
  " }}}

  " }}}

  " Motion {{{

  NeoBundlewLazy 'Lokaltog/vim-easymotion', {
    \ 'mappings': '<Plug>'
    \ }

  NeoBundlew 'nelstrom/vim-visual-star-search'

  " }}}

  " Utility {{{

  NeoBundlew 'rbgrouleff/bclose.vim'

  NeoBundlew 'thinca/vim-localrc'

  NeoBundlewLazy 'thinca/vim-quickrun', {
    \ 'commands': ['QuickRun']
    \ }

  NeoBundlewLazy 'kannokanno/previm', {
    \ 'depends': ['open-browser'],
    \ 'filetypes': ['markdown']
    \ }

  NeoBundlewLazy 'tyru/restart.vim', {
    \ 'gui': 1,
    \ 'commands': ['Restart']
    \ }

  NeoBundlewLazy 'tyru/open-browser.vim', {
    \ 'mappings': '<Plug>(openbrowser-'
    \ }

  NeoBundlewLazy 'ryym/macspeech.vim', {
    \ 'disabled': !g:is_mac,
    \ 'commands': ['MacSpeech', 'MacSpeechSelected']
    \ }

  " }}}

  " VCS {{{

  NeoBundlewLazy 'tpope/vim-fugitive', {
    \ 'commands': ['Gstatus', 'Git', 'Gdiff']
    \ }

  " }}}

  " Filetype {{{

  NeoBundlewLazy 'mattn/emmet-vim', {
    \ 'filetypes': ['html', 'xml', 'eruby', 'jsp']
    \ }
  NeoBundlewLazy 'PProvost/vim-ps1', {
    \ 'filetypes': ['ps1']
    \ }

  NeoBundlewLazy 'kchmck/vim-coffee-script', {
    \ 'filetypes': ['coffee']
    \ }

  NeoBundlewLazy 'bruno-/vim-man', {
    \ 'disabled': g:is_windows,
    \ 'mappings': ['<Plug>(Man)', '<Plug>(Sman)', '<Plug>(Vman)'],
    \ 'commands': ['Man', 'Sman', 'Vman']
    \ }

  " }}}

  " UI {{{

  NeoBundlew 'itchyny/lightline.vim'

  NeoBundlew 'nathanaelkane/vim-indent-guides'

  NeoBundlew 'w0ng/vim-hybrid'

  " }}}

  " Others {{{

  NeoBundlew 'tpope/vim-unimpaired'

  " }}}

endfunction " }}}

" Configure bundles {{{
function! s:configure_bundles(neobundle_wrapper)
  let nbw = a:neobundle_wrapper

  " Extension {{{

  " unite and extentions {{{

  "   unite {{{
  if nbw.tap('unite')
    MapNamedKey unite   <Space>u
    MapNamedKey uniteNq <Space>U

    let s:mappings = [
      \   ['u', 'Unite'                    , '<Space>'],
      \   ['b', 'Unite buffer_tab'         , '<CR>'],
      \   ['f', 'Unite file'               , '<CR>'],
      \   ['r', 'Unite file_rec'           , '<CR>'],
      \   ['o', 'Unite'                    , ' output:'],
      \   ['c', 'UniteWithCurrentDir file' , '<CR>'],
      \   ['C', 'UniteWithCurrentDir'      , '<Space>'],
      \   ['d', 'UniteWithBufferDir file'  , '<CR>'],
      \   ['D', 'UniteWithBufferDir'       , '<Space>'],
      \   ['k', 'UniteWithCursorWord line' , '<CR>'],
      \   ['K', 'UniteWithCursorWord'      , '<Space>']
      \ ]
    for [s:k, s:command, s:end] in s:mappings
      execute 'Map n' '\[unite]'   . s:k ':u:' . s:command . s:end
      execute 'Map n' '\[uniteNq]' . s:k ':u:' . s:command '-no-quit -winheight=15' . s:end
    endfor
    unlet s:mappings s:k s:command s:end

    function! nbw.hooks.on_source(_)
      " Show dotfiles at :Unite file
      call unite#custom#source('file', 'matchers', 'matcher_default')

      call unite#custom#profile('default', 'context', {
        \ 'start_insert': 1
        \ })

      let g:unite_source_alias_aliases = {
        \ 'f'  : 'file',
        \ 'fr' : 'file_rec',
        \ 'b'  : 'buffer',
        \ 'bt' : 'buffer_tab',
        \ 'g'  : 'grep',
        \ 'l'  : 'line',
        \ 'nb' : 'neobundle'
        \ }

      if executable('ag')
        let g:unite_source_rec_async_command = 'ag --follow --nocolor --nogroup --hidden -g ""'
        let g:unite_source_grep_comand = 'ag'
      endif

      " Key mappings in unite buffers
      autocmd vimrc FileType unite call <SID>map_keys_on_unite()
      function! s:map_keys_on_unite()
        Map ni (buffer expr) s unite#smart_map('s', unite#do_action('split'))
        Map ni (buffer expr) v unite#smart_map('v', unite#do_action('vsplit'))
        Map ni (buffer expr) f unite#smart_map('f', unite#do_action('vimfiler'))
      endfunction
    endfunction
  endif
  "   }}}

  "   unite-mru {{{
  if nbw.tap('neomru')
    function! nbw.hooks.on_source(_)
      let g:unite_source_alias_aliases['fm'] = 'file_mru'
    endfunction
  endif
  "   }}}

  " }}}

  " vimfiler {{{
  if nbw.tap('vimfiler')
    MapNamedKey vimfiler <Space>f
    Map n \[vimfiler]f :us:VimFiler
    Map n \[vimfiler]s :us:VimFiler -split -winwidth=60
    Map n \[vimfiler]c ::VimFilerCurrentDir
    Map n \[vimfiler]d ::VimFilerBufferDir
    Map n \[vimfiler]e ::VimFilerBufferDir -split -simple -winwidth=35 -no-quit
    Map n \[vimfiler]E :us:VimFiler        -split -simple -winwidth=35 -no-quit

    function! nbw.hooks.on_source(_)
      let g:vimfiler_as_default_explorer  = 1
      let g:vimfiler_safe_mode_by_default = 0

      " Key mappings in vimfiler buffers
      autocmd vimrc FileType vimfiler call <SID>map_keys_on_vimfiler()
      function! s:map_keys_on_vimfiler()
        " TODO: This conflicts with the default mapping '<Space>'(mark file).
        Remap n (buffer) <Space>q <Plug>(vimfiler_exit)

        Remap n (silent buffer expr) Ar vimfiler#do_action('rec')
      endfunction

    endfunction
  endif
  " }}}

  " vimshell {{{
  if nbw.tap('vimshell')
    MapNamedKey vimshell <Space>s
    Map n \[vimshell]s :us:VimShell
    Map n \[vimshell]c ::VimShellCurrentDir
    Map n \[vimshell]d ::VimShellBufferDir
  endif
  " }}}

  " submode {{{
  if nbw.tap('submode')
    function! nbw.hooks.on_source(_)
      let g:submode_keep_leaving_key = 1
      let g:submode_timeout          = 0
      let g:submode_timeoutlen       = 3000
    endfunction

    function! nbw.hooks.on_post_source(_)
      " Submode scroll {{{
      call w#submode#define('scroll')
      SbmScrollEnter n <Leader>s
      SbmScroll n <r> u <C-u>
      SbmScroll n <r> d <C-d>
      SbmScroll n <r> f <C-f>
      SbmScroll n <r> b <C-b>
      SbmScroll n j     2<C-e>j
      SbmScroll n k     2<C-y>k
      SbmScroll n J     4<C-e>j
      SbmScroll n K     4<C-y>k
      SbmScroll n <C-j> 6<C-e>j
      SbmScroll n <C-k> 6<C-y>k
      " }}}

      " Submode window-resize {{{
      call w#submode#define('winRes')
      SbmWinResEnter n <C-w>m
      SbmWinResEnter n <C-w><C-m>

      " 'L'ower, 'H'eighten, 'S'horten, 'W'iden
      for [s:k, s:command] in items({
        \ 'l': '<C-w>-',
        \ 'h': '<C-w>+',
        \ 's': '<C-w><',
        \ 'w': '<C-w>>'
        \ })
        execute 'SbmWinRes n' s:k               1 . s:command
        execute 'SbmWinRes n' toupper(s:k)      3 . s:command
        execute 'SbmWinRes n' '<C-' . s:k . '>' 5 . s:command
      endfor
      unlet s:k s:command
      " }}}
    endfunction
  endif
  " }}}

  " }}}

  " Editing {{{

  " easy-align {{{
  if nbw.tap('easy-align')
    Remap nv ga <Plug>(EasyAlign)
  endif
  " }}}

  " yankround {{{
  if nbw.tap('yankround')
    Remap nx p     <Plug>(yankround-p)
    Remap n  P     <Plug>(yankround-P)
    Remap nx gp    <Plug>(yankround-gp)
    Remap n  gP    <Plug>(yankround-gP)
    Remap n <C-p> <Plug>(yankround-prev)
    Remap n <C-n> <Plug>(yankround-next)
  endif
  " }}}

  " operator-replace {{{
  if nbw.tap('operator-replace')
    Remap nvo mr <Plug>(operator-replace)
  endif
  " }}}

  " smartinput-endwise {{{
  if nbw.tap('smartinput-endwise')
    function! nbw.hooks.on_source(_)
      call smartinput_endwise#define_default_rules()
    endfunction
  endif
  " }}}

  " }}}

  " Motion {{{

  " EasyMotion {{{
  if nbw.tap('easymotion')
    Remap nvo ms <Plug>(easymotion-s2)
    Remap nvo mf <Plug>(easymotion-fl2)
    Remap nvo mF <Plug>(easymotion-Fl2)
    Remap nvo mt <Plug>(easymotion-tl2)
    Remap nvo mT <Plug>(easymotion-Tl2)
    Remap nvo m/ <Plug>(easymotion-fn)
    Remap nvo m? <Plug>(easymotion-Fn)
    Remap nvo m; <Plug>(easymotion-next)
    Remap nvo m, <Plug>(easymotion-prev)

    function! nbw.hooks.on_source(_)
      " Disable default mappings.
      let g:EasyMotion_do_mapping = 0

      let g:EasyMotion_space_jump_first = 1
      let g:EasyMotion_smartcase        = 1
      let g:EasyMotion_use_upper        = 1
      let g:EasyMotion_keys = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'

      highlight link EasyMotionIncSearch    Search
      highlight link EasyMotionTarget       ErrorMsg
      highlight link EasyMotionShade        Comment
      highlight link EasyMotionTarget2First Todo
    endfunction
  endif
  " }}}

  " }}}

  " Utility {{{

  " bclose {{{
  if nbw.tap('bclose')
    Map n \[buffer]d ::Bclose
    Map n \[buffer]D ::Bclose!
  endif
  " }}}

  " quick-run {{{
  if nbw.tap('quickrun')
    Map nv <Leader>r :r:QuickRun
  endif
  " }}}

  " open-browser {{{
  if nbw.tap('open-browser')
    Remap nv <Leader>obo <Plug>(openbrowser-open)
    Remap nv <Leader>obs <Plug>(openbrowser-search)
  endif
  " }}}

  " restart {{{
    if nbw.try_tap('restart')
      " This doesn't work under the terminal.
      Map n <Leader><Leader>r ::Restart
      Map n <Leader><Leader>R ::Restart!
    endif
  " }}}

  " macspeech {{{
  if nbw.try_tap('macspeech')
    Map v  <Leader><Leader>q :r:MacSpeechSelected
    Map nv <Leader><Leader>Q ::MacSpeechStop
  endif
  " }}}

  " }}}

  " VCS {{{

  " fugitive {{{
  if nbw.tap('fugitive')
    MapNamedKey git <Space>g
    Map nv \[git]g :s:Git
    Map nv \[git]s ::Gstatus
    Map nv \[git]d ::Gdiff

    function! nbw.hooks.on_post_source(bundle)
      " Detect current opened file to enable fugitive.
      call fugitive#detect(expand('#:p'))
    endfunction
  endif
  " }}}

  " }}}

  " FileType {{{

  " man (man page) {{{
  if nbw.try_tap('man')
    MapNamedKey man qm
    Map n \[man]m :s:Vman
    Remap n \[man]w <Plug>(Vman)
  endif
  " }}}

  " }}}

  " UI {{{

  " indent-guides {{{
  if nbw.tap('indent-guides')
    Remap n \[toggle]i <Plug>IndentGuidesToggle

    function! nbw.hooks.on_source(_)
      let g:indent_guides_start_level = 2
      let g:indent_guides_guide_size  = 1
      let g:indent_guides_exclude_filetypes = ['help', 'man']
      if g:is_gui
        autocmd vimrc VimEnter * IndentGuidesEnable
      endif
    endfunction
  endif
  " }}}

  " lightline {{{
  if nbw.tap('lightline')
    if ! has('vim_starting')
      " Override default statusline when vimrc is reloaded.
      call lightline#update()
    endif
  endif
  " }}}

  " }}}

endfunction " }}}

" Load bundles by NeoBundle. {{{

call w#neobundle#execute({
  \   'vimrc'             : $MYVIMRC,
  \   'bundle_dir'        : $MYVIMDIR  . '/bundle',
  \   'declare_bundles'   : function('s:declare_bundles'),
  \   'configure_bundles' : function('s:configure_bundles')
  \ })
delfunction s:declare_bundles
delfunction s:configure_bundles

" }}}

" Another plugins {{{

runtime macros/matchit.vim

" }}}

if ! has('vim-starting')
  " Todo: neobundle call on_source
endif

" }}}

" Local settings {{{

if filereadable($VIMLOCAL . '/vimrc')
  source $VIMLOCAL/vimrc
endif

" }}}

" vim: expandtab softtabstop=2 shiftwidth=2 foldmethod=marker
